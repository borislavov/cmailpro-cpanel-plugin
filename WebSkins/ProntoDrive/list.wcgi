// ============================================= //
//                   List files                  //
//                                               //
// Version 1.0                                   //
// Anton Katsarov <anton@webface.bg>             //
// ============================================= //
//
// Description:
//  Lists Accounts files and folders
//

function sizeReadable(size, type) forward;
function dateReadable(theDate) forward;

function list() is
    // Check if ProntoDrive Folder exists
    rootFolder = ListStorageFiles("");
    baseFound = false;
    for i = 0  while i < Length(rootFolder) by i+= 1 loop
      if !IsNumber(rootFolder.(rootFolder[i]).STFileSize) && rootFolder[i] == "ProntoDrive" then
        baseFound = true;
      end if;
      exitif baseFound;
    end loop;
    if !baseFound then
      creationResult = CreateStorageDirectory("ProntoDrive");
    end if;

    //List Folders
    theSet = NewDictionary();
    theSet.Folders = NewArray();
    theSet.Files = NewArray();
    folder = "ProntoDrive";
    path = GetHTTPParameter("path",0);
    if IsString(path) && path != "" && path != " " then
      folder = folder + "/" + path;
      theSet.path = path;
      path = String(FindRegEx(path,"^.+/"));
      path = Substring(path, 2, Length(path) - 5);
      if !IsString(path) then
        path = " ";
      end if;
      theSet.Upper = path;
    end if;
    listedFolder = ListStorageFiles(folder);
    for i = 0 while i < Length(listedFolder) by i+=1 loop
      theElement = NewDictionary();
      theElement.Size = sizeReadable(listedFolder.(listedFolder[i]).STFileSize, "B");
      theElement.Modified = dateReadable(listedFolder.(listedFolder[i]).STModified);
      theElement.Name = listedFolder[i];
      theElement.Path = theSet.path || " ";
      ext = String(FindRegEx(ToLowerCase(listedFolder[i]),"[.][a-z0-9]+$"));
      theElement.Ext = Substring(ext, 2, Length(ext) - 3);
      if IsNumber(listedFolder.(listedFolder[i]).STFileSize) then
        theSet.Files.addElement(theElement);
      else
        theSet.Folders.addElement(theElement);
      end if;
    end loop;
    return theSet;
end function;

function sizeReadable(size, type) is
  if IsNumber(size) then
    if size > 1024 then
      localType = "B";
      if type == "B" then
        localType = "kB";
      elif type == "kB" then
        localType = "MB";
      elif type == "MB" then
        localType = "GB";
      elif type == "GB" then
        localType = "TB";
      end if;
      size = Number(sizeReadable(size/1024, localType));
      return String(size) + localType;
    else
    return String(size) + type;
    end if;
  else
    return "O" + type;
  end if;
end function;

function dateReadable(theDate) is
  stringDate = String(theDate);
  return Substring(stringDate,0,10) + " " + Substring(stringDate,11,20);
end function;
